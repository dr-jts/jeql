/*=============================
Visualize a grid dataset as a SliceGraph (AKA LneGraph)
=============================*/

DATA_SCALE = .008; 

TextReader t file: "world_pop_lores.grid";

//--- convert grid file to individual coordinate data rows
RE_NUM = \"[\d\.]+";
t = select rowNum() y, line from t;
t = select y, splitIndex x, Val.toDouble(splitValue) data from t split by RegEx.splitByMatch(line, RE_NUM);

//--- scale x,y,data to image size; raise non-zero data by a small amount for appearance
t = select 10 * (172 - y) y ,
	6 * x x ,
	( data > 0 ?    2.0 + data * DATA_SCALE    : 0.0  ) data 
	from t;

//--- create slice lines
t = select y, x, geomConnect( Geom.createPoint(  x, y + data ) ) line from t group by y;

//--- create masking polygon, base lines for rendering
//--- include symbolization, and sort geometry for rendering
t = select y, 
  styleStroke,  2 styleStrokeWidth,  #ffffff styleFill,
	case y2 
	 when #poly then Geom.createPolygon ( Geom.collect ( line, LINESTRING( 2000 1, 1 1) ) )
	 when #line then Geom.difference( line, baseline ) 
	 when #water then Geom.intersection( line,  baseline )
	end geom
	let 
		baseline = Geom.createLine(0, y, 3000, y )
	from t 
	join [ tt( y2, styleStroke ) [ #poly, null ] [ #line, #222222  ]  [ #water, #EEEEEE ] ] tt
	order by y desc;

//Print t;

Plot data: t
	width: 1800 
	height: 1450
	extent: LINESTRING (0 0, 2540 1800 )
	file: "world_pop_slicegraph.png";
